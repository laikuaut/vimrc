"""""""""""""""""""""""""""""""""""""""""""""""""
" 基本設定
"""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
" 文字コード
if v:lang =~ "utf8$" || v:lang =~ "UTF-8$"
   set fileencodings=ucs-bom,utf-8,latin1
endif
" vim のデフォルト設定にする
set nocompatible
""" }}}

"""""""""""""""""""""""""""""""""""""""""""""""""
" viminfo
"""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
set viminfo='20,\"50 " read/write a .viminfo file, don't store more
set history=50 " keep 50 lines of command line history
""" }}}

"""""""""""""""""""""""""""""""""""""""""""""""""
" カラースキーマを設定
"""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
""" 全角スペースの可視化 {{{
" 参考 : http://qiita.com/kojionilk/items/67379e68cf54d811081a
augroup highlightIdegraphicSpace
  autocmd!
  autocmd Colorscheme * highlight link IdeographicSpace Todo
  autocmd VimEnter,WinEnter * call matchadd("IdeographicSpace", '　')
augroup END " }}}

""" MarkDownの簡易ハイライト設定 " {{{
function! g:auto_vimrc_simple_markdown_hightlight() " {{{
  augroup highlightSimpleMarkdown
    autocmd!
    " シンタックス範囲定義
    autocmd VimEnter,WinEnter *.md syn sync minlines=500 maxlines=1000
    " コードブロック
    autocmd VimEnter,WinEnter *.md syn match mdCodeDelimiter "^```.*" contained
    autocmd VimEnter,WinEnter *.md syn region mdCode start="^\s*```.*" end="^\s*```" keepend contains=mdCodeDelimiter
    " 見出し
    autocmd VimEnter,WinEnter *.md syn match mdH "^#\+.\+$"
    " リスト
    autocmd VimEnter,WinEnter *.md syn match mdList "^ *[\-\+\*] "
    " 順序付きリスト
    autocmd VimEnter,WinEnter *.md syn match mdNumList "^ *\([0-9]\+\.\)\+ "
    " リンク
    autocmd VimEnter,WinEnter *.md syn match mdLinkDelimiter "[()\[\]]" contained
    autocmd VimEnter,WinEnter *.md syn region mdId start="\[\(?=.+?\](\)" end="\]" keepend nextgroup=mdLink skipwhite contains=mdLinkDelimiter
    autocmd VimEnter,WinEnter *.md syn region mdLink start="(" end=")" keepend contained contains=mdLinkDelimiter
    " 引用
    autocmd VimEnter,WinEnter *.md syn match mdBlockQuote "^> .\+$"
    " カラースキーマ定義
    autocmd Colorscheme * highlight link mdH Title
    autocmd Colorscheme * highlight link mdCodeDelimiter Special
    autocmd Colorscheme * highlight link mdList CursorLineNr
    autocmd Colorscheme * highlight link mdNumList CursorLineNr
    autocmd Colorscheme * highlight link mdCode Comment
    autocmd Colorscheme * highlight link mdBlockQuote Constant
    autocmd Colorscheme * highlight link mdLink Underlined
    autocmd Colorscheme * highlight link mdId Type
    autocmd Colorscheme * highlight link mdLinkDelimiter cleaned
  augroup END
endfunction " }}}
" 呼び出し
call g:auto_vimrc_simple_markdown_hightlight()
" }}}

" シンタックスのカラー表示をON
syntax on

""" デフォルトで使用可能なカラースキーム {{{
"colorscheme delek
"colorscheme koehler
"colorscheme peachpuff
"colorscheme torte
"colorscheme blue
colorscheme desert
"colorscheme morning
"colorscheme ron
"colorscheme zellner
"colorscheme darkblue
"colorscheme elflord
"colorscheme murphy
"colorscheme shine
"colorscheme default
"colorscheme evening
"colorscheme pablo
"colorscheme slate
""" }}}
""" }}}

""""""""""""""""""""""""""""""""""""""""""""""""
" vimdiff
""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
" vimdiffの色設定
" 参考 : http://qiita.com/takaakikasai/items/b46a0b8c94e476e57e31
highlight DiffAdd    cterm=bold ctermfg=10 ctermbg=22
highlight DiffDelete cterm=bold ctermfg=10 ctermbg=52
highlight DiffChange cterm=bold ctermfg=10 ctermbg=17
highlight DiffText   cterm=bold ctermfg=10 ctermbg=21
""" }}}

"""""""""""""""""""""""""""""
" インデント設定
"""""""""""""""""""""""""""""
""" {{{
" ＜TAB>入力時、空白を挿入にする
set expandtab
" タブ表示幅を設定
set tabstop=4
" <TAB>入力時、挿入する文字数を設定
set softtabstop=4
" 自動インデントを行った際、設定する空白数
set shiftwidth=4
" コンテキストに応じたタブの処理を行なう
set smarttab
" 改行時に前の行の行末を見て自動でインデント
"set smartindent
" インデント自動的
set autoindent
""" }}}

"""""""""""""""""""""""""""""
" 画面表示
"""""""""""""""""""""""""""""
""" {{{
" 行番号を表示
set number
" 相対行番号で表示
"set relativenumber
" 不可視文字を表示
set list
" 不可視文字の表示文字を設定
set listchars=tab:>-,trail:_
" ステータスラインの設定
set statusline=%F%m%r%h%w\ [FORMAT=%{&ff}]\ [TYPE=%Y]\ [ASCII=\%03.3b]\ [HEX=\%02.2B]\ [POS=%04l,%04v][%p%%]\ [LEN=%L]
" ステータスラインを常に表示
set laststatus=2
" 行を折り返す
set wrap
" 強調表示する列を設定する
"set colorcolumn=80
" 編集中のファイル名を表示しない
set notitle
" 括弧の対応をハイライト
set showmatch
" 現在のモードを表示
set showmode
" 入力中のコマンドを表示する(例えば yy を実行する際に yだけ入力すると「y」が右下に表示される。)
set showcmd
" カーソルラインを有効化
"set cursorline
" 列を強調表示
"set cursorcolumn
" カーソルラインを色なしに設定(行番号のみをハイライト)
"autocmd ColorScheme * highlight clear CursorLine
" ルーラー表示
"set ruler
""" }}}

"""""""""""""""""""""""""""""
" 検索設定
"""""""""""""""""""""""""""""
""" {{{
" 検索結果をハイライト表示する
set hlsearch
" インクリメンタルサーチを有効にする
set incsearch
" 検索時に最後まで移動したら最初に戻る
set wrapscan
""" }}}

"""""""""""""""""""""""""""""
" 移動設定
"""""""""""""""""""""""""""""
""" {{{
" 行頭・行末の左右移動で行を移動する
set whichwrap=b,s,h,l,<,>,[,]
" BSで削除できるものを指定する
" indent  : 行頭の空白
" eol     : 改行
" start   : 挿入モード開始位置より手前の文字
set bs=indent,eol,start
""" }}}

"""""""""""""""""""""""""""""
" コマンドモード
"""""""""""""""""""""""""""""
""" {{{
" コマンド補完を強化
set wildmenu
" リスト表示，最長マッチ
set wildmode=list:longest
""" }}}

"""""""""""""""""""""""""""""
" その他
"""""""""""""""""""""""""""""
""" {{{
" 8進数を無効にする。<C-a>,<C-x>に影響する
set nrformats-=octal
" 日本語の行の連結時には空白を入力しない
set formatoptions+=Mm
" 折り畳みを設定「{{{ }}}で囲う」
" za 部分開閉
" zr 全開き
" zm 全閉じ
set foldmethod=marker
" 折り畳み階層指定
set foldlevel=1
""" }}}

"""""""""""""""""""""""""""""""""""""""""""""""""
" キーマップを設定
"""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
"nmap h <Insert>
"nmap i <Up>
"nmap j <Left>
"nmap k <Down>
"nmap l <Right>
nmap scb <C-w><Left>gg:set scrollbind<CR>:set nowrap<CR><C-w><Right>gg:set scrollbind<CR>:set nowrap<CR><C-w><Left>
""" }}}

"""""""""""""""""""""""""""""""""""""""""""""""""
" インデント用関数
"""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
" インデントOFF
function! g:noIndent() " {{{
    setlocal shiftwidth=0
    setlocal softtabstop=0
    setlocal nosmarttab
    setlocal noautoindent
    setlocal nosmartindent
    setlocal formatoptions-=r
    setlocal paste
endfunction " }}}

" インデントON
function! g:indent() " {{{
    setlocal smarttab
    setlocal autoindent
"    setlocal smartindent
    setlocal formatoptions+=r
    setlocal shiftwidth=4
    setlocal softtabstop=4
    setlocal nopaste
endfunction " }}}
""" }}}

"""""""""""""""""""""""""""""""""""""""""""""""""
" タブラインを設定
" 参考: http://qiita.com/wadako111/items/755e753677dd72d8036d
"""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
" Anywhere SID.
function! s:SID_PREFIX() " {{{
  return matchstr(expand('<sfile>'), '<SNR>\d\+_\zeSID_PREFIX$')
endfunction " }}}

" Set tabline.
function! s:my_tabline()  "{{{
  let s = ''
  for i in range(1, tabpagenr('$'))
    let bufnrs = tabpagebuflist(i)
    let bufnr = bufnrs[tabpagewinnr(i) - 1]  " first window, first appears
    let no = i  " display 0-origin tabpagenr.
    let mod = getbufvar(bufnr, '&modified') ? '!' : ' '
    let title = fnamemodify(bufname(bufnr), ':t')
    let title = '[' . title . ']'
    let s .= '%'.i.'T'
    let s .= '%#' . (i == tabpagenr() ? 'TabLineSel' : 'TabLine') . '#'
    let s .= no . ':' . title
    let s .= mod
    let s .= '%#TabLineFill# '
  endfor
  let s .= '%#TabLineFill#%T%=%#TabLine#'
  return s
endfunction "}}}
" タブラインの表示設定
let &tabline = '%!'. s:SID_PREFIX() . 'my_tabline()'
" 常にタブラインを表示
set showtabline=2
""" }}}

"""""""""""""""""""""""""""""""""""""""""""""""""
" カーソルラインとカラムのハイライト設定
" 参考: http://thinca.hatenablog.com/entry/20090530/1243615055
"       http://qiita.com/koara-local/items/57b5f2847b3506a6485b
"""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
" カーソルラインのグループをONにする
function! g:auto_vimrc_auto_cursorline() " {{{
    augroup vimrc-auto-cursorline
        autocmd!
        autocmd InsertLeave,CursorMovedI * call s:auto_cursorline('off')
        autocmd InsertEnter,VimEnter,WinEnter,CursorHold,CursorHoldI * call s:auto_cursorline('on')
    augroup END
endfunction " }}}
" カーソルラインのグループをOFFにする
function! g:noauto_vimrc_auto_cursorline() " {{{
    augroup vimrc-auto-cursorline
        autocmd!
        call s:auto_cursorline('off')
    augroup END
endfunction " }}}
" カーソルライングループ用関数定義
function! s:auto_cursorline(event) " {{{
    if a:event ==# 'off'
        hi clear CursorLine
        setlocal nocursorcolumn
        setlocal colorcolumn=0
    elseif a:event ==# 'on'
        setlocal cursorcolumn
        setlocal cursorline
        hi CursorLine term=underline cterm=underline guibg=Grey90
        setlocal colorcolumn=80
    endif
endfunction " }}}
" 呼び出し
call g:auto_vimrc_auto_cursorline()
""" }}}

"""""""""""""""""""""""""""""""""""""""""""""""""
" コマンドコード：相対的な番号と絶対的な行番号をトグル
" 挿入モード：絶対的な番号
"""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
" 相対行番号のグループをONにする
function! g:auto_vimrc_auto_relativenumber() " {{{
    augroup vimrc-auto-relativenumber
        autocmd!
        autocmd InsertEnter,CursorHold,CursorHoldI,VimEnter * call s:toggleNumberOption('on')
        autocmd InsertLeave,CursorMovedI * call s:toggleNumberOption('off')
    augroup END
endfunction "}}}
" 相対行番号のグループをOFFにする
function! g:noauto_vimrc_auto_relativenumber() " {{{
    augroup vimrc-auto-relativenumber
        autocmd!
        call s:toggleNumberOption('off')
    augroup END
endfunction " }}}
" 行番号グループ用関数定義
function! s:toggleNumberOption(event) "{{{
    if a:event ==# 'on'
        if&number
            setlocal relativenumber
        else
            setlocal number
        endif
    elseif a:event ==# 'off'
        if&number
            setlocal norelativenumber
        else
            setlocal number
        endif
    endif
endfunction "}}}
" 呼び出し
"call g:auto_vimrc_auto_relativenumber()
""" }}}

""""""""""""""""""""""""""""""""""""""""""""""""""
"" 貼り付け時、自動でインデント無効化(pasteモード)
"" 参考 : http://qiita.com/ryoff/items/ad34584e41425362453e
""""""""""""""""""""""""""""""""""""""""""""""""""
"""" {{{
"if &term =~ "xterm"
"    let &t_ti .= "\e[?2004h"
"    let &t_te .= "\e[?2004l"
"    let &pastetoggle = "\e[201~"
"
"    function! s:XTermPasteBegin(ret)
"        set paste
"        return a:ret
"    endfunction
"
"    noremap <special> <expr> <Esc>[200~ s:XTermPasteBegin("0i")
"    inoremap <special> <expr> <Esc>[200~ s:XTermPasteBegin("")
"    cnoremap <special> <Esc>[200~ <nop>
"    cnoremap <special> <Esc>[201~ <nop>
"endif
"""" }}}

""""""""""""""""""""""""""""""""""""""""""""""""
" FileType定義
""""""""""""""""""""""""""""""""""""""""""""""""
""" {{{
"autocmd BufRead,BufNewFile *.py setfiletype python
""" }}}
